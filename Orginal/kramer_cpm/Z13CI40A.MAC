;TASTATURSTEUERUNG
HWMODI	equ	4		;LATCHADRESSE HARDWAREMODIF.
ZKPOS	equ	4		;ZEITK.POSITIV-ENTPRELLUNG
ZKNEG	equ	4		;ZEITK.NEGATIV-ENTPRELLUNG
K7659	EQU	1		;1 BEI TASTATUR K7659
K7652	EQU	0		;1 BEI TASTATUR K7652
;
;************************************************************
;--TASTATURABFRAGE FUER K 7652/59 MIT HARDWARE NACH MP 7/88-- 
;************************************************************
;
;MODEFLAGS
;BIT 0 : LANGE REPEATVERZ.DURCHLAUFEN
;    1 : STRINGAUSGABE
;    2 : GRAFIKMODE ON
;    3 : LEER
;    4 : CAPS
;    5 : TASTATURBEEP ON
;    6 : LEER
;    7 : TASTE BETAETIGT
;
;TASTATURSTATUS
;
CONST:	DI
	PUSH	HL
	PUSH	DE
	PUSH	BC
	LD	A,0FH
	OUT	(LATCH),A
	LD	HL,MOFL
	CALL	MINIT
	LD	HL,MLZ
	LD	M,0
	OR	A
	POP	BC
	POP	DE
	POP	HL
	EI
	RET	Z
	LD	A,0FFH		;TASTE GEDR.
	RET

;
;ZEICHEN VON TASTATUR 'A' MIT WARTEN AUF BETAETIGUNG
;
CONIN:	DI
	PUSH	HL
	PUSH	DE
	PUSH	BC
	CALL	TAST
	OR	A
	CALL	Z,SETO
	POP	BC
	POP	DE
	POP	HL
	EI
	JR	Z,CONIN
	RET
;
SETO:	PUSH	AF
	XOR	A
	LD	(MLZ),A
	POP	AF
	RET
;
;TASTATURPOLLING
; 
TAST:	LD	A,0FH
	OUT	(LATCH),A
	LD	HL,MOFL
	BIT	1,M		;STRINGMODE?
	JP	NZ,AUSG$
MINIT:	IN	A,(PIOBD)
	CPL
	AND	0FH		;MASKE
	JR	NZ,MSTA1
	RES	0,M		;RUECKSETZEN REPEATKENNZ.
	RES	7,M		;KEINE BETAETIGUNG
	LD	(MLZ),A
	OUT	(LATCH),A
	RET
MSTA1:	LD	A,(MLZ)		;LETZTES ZEICHEN=0?
	OR	A
	JR	Z,MP1
	BIT	0,M		; =1 WENN VORHER REPEAT
Š	JR	Z,MR0
;
;KLEINE REPEATVERZ.
;
	LD	B,28
MKV1:	LD	C,0
MKV2:	DEC	C
	JR	NZ,MKV2
	DJNZ	MKV1
	JR	MP1
;
;GROSSE REPEATVERZ.+NEGATIV ENTPRELLUNG
; 
MR0:	LD	B,080H
MR1:	LD	C,0
MR2:	IN	A,(PIOBD)
	CPL
	AND	0FH
	JR	Z,MR3
	DEC	C
	JR	NZ,MR2
	DJNZ	MR1
	SET	0,M
	JR	MP1
MR3:	LD	B,ZKNEG		;NEG. ENTPRELLZ. B*5ms
MR4:	LD	C,00H
MR5:	IN	A,(PIOBD)
	CPL
	AND	0FH
	JR	NZ,MR0
	DEC	C
	JR	NZ,MR5
	DJNZ	MR4
;
;SPALTENABTESTUNG
; 
MP1:	BIT	7,M		;ENTPRELLUNG?
	JR	NZ,MP0
	LD	B,ZKPOS		;POS-ENTPRELLZ. B*5ms
ENTPR:	LD	C,80H
ZS11:	IN	A,(PIOBD)
	CPL
	AND	0FH
	RET	Z
	DEC	C
	JR	NZ,ZS11
	DJNZ	ENTPR
MP0:	XOR	A
MP5:	CP	7
	JR	NZ,MP2
	ADD	A,1
MP2:	OUT	(LATCH),A
	LD	D,A
MP3:	IN	A,(PIOBD)
	CPL
	AND	0FH
	JR	NZ,MP4
	LD	A,D
	ADD	A,1H
	CP	12		;12 SPALTEN
	JR	NZ,MP5
	RES	0,M
FZUE:	XOR	A
	RET
;
;ERMITTLUNG TASTENPOSITION
; 
MP4:	LD	BC,708H
	OUT	(C),B		;SPALTE 7 AKT.
	SLA	D
	SLA	D
	SLA	D		;MULT.SPALTE MIT 8
	DEC	A
	ADD	A,D
	LD	E,A		;E=INTERNER TASTENCODE 0-95
;
;AUSWERTUNG SHIFT
; 
	LD	(MLZ),A
	IN	A,(PIOBD)
	AND	0FH
	XOR	8
	LD	A,E
	JR	NZ,FKTZU
	ADD	A,60H
	LD	E,A
;
;FUNKTIONSZUORDNUNG
;
FKTZU:	LD	HL,(FTPOI)	;FUNKT.-TASTENTAB.-POINTER
	LD	BC,(LFTB)	;LAENGE TABELLE
	CPIR			;SUCHEN TAST.POS.
	JR	NZ,MP21
	DEC	HL
	LD	BC,(FTPOI)
	SBC	HL,BC		;HL:=GEF.POS.REL.
	SLA	L		;*2
	LD	BC,(FTAPO)	;F-TASTEN-ADRESS-TB
	ADD	HL,BC
	LD	C,M
	INC	HL
	LD	H,M
	LD	L,C		;HL=UP-ADRESSE
	LD	BC,MOFL		;MODEFLAGS
	LD	A,(BC)
	RES	0,A		;LOESCHEN LANGE REPEAT
	BIT	7,A
	JP	NZ,FZUE
	SET	7,A
	LD	(BC),A
	JP	(HL)
;
J100:	LD	SP,80H
	JP	100H
GRF:	LD	L,4	
	JR	AUSF	
BEEP:	LD	L,20H	
	JR	AUSF
SLOCK:	LD	L,10H	
AUSF:	LD	A,(BC)
	XOR	L	
	LD	(BC),A
	XOR	A
	LD	E,A	
	JP	SETFF
MONRET:	XOR	A
	OUT	(HWMODI),A
	JP	0F000H		;ROM START
B3264:	LD	L,80H	
	JR	AUSF1
ZGUM:	LD	L,20H	
AUSF1:	IN	A,(HWMODI)
	XOR	L
	JR	AUSF2
F2MHZ:	IN	A,(HWMODI)
	AND	0BFH
	JR	AUSF2
F4MHZ:	IN	A,(HWMODI)
	OR	40H
AUSF2:	OUT	(HWMODI),A
AUSF3:	XOR	A
	LD	(MLZ),A
	RET
;
;ZEICHEN AUS TABELLE
;
MP21:	LD	HL,(TAPOI)
	LD	D,0
	ADD	HL,DE		;BERECHN. TABELLENPOS.
	LD	A,M		;HOLEN ZEICHEN TABELLE 
	LD	E,A
;
;CONTROL FUNKTION
;
	IN	A,(PIOBD)
	BIT	3,A		;ABFRAGE CTRL-TASTE
	JR	NZ,F$		;NEIN
	LD	A,E
	AND	9FH
	LD	E,A
	IN	A,(PIOBD)
	AND	0FH
	JR	NZ,F$
	LD	A,80H
	ADD	A,E
	LD	E,A
;
;STRING FUNKTION
;
F$:	BIT	7,E
	JR	Z,MNULL	
	LD	(MLZ),A	
	LD	HL,(PO$TA)
F$1:	LD	A,M
	OR	A	
	JR	Z,MNULL	
	INC	HL	
	CP	E
	JR	NZ,F$1	
	LD	(NEXT$),HL
AUSG$:	LD	HL,(NEXT$)	;NAECHSTES STR.ZEICHEN
	LD	A,M	
	INC	HL
	LD	(NEXT$),HL
	LD	HL,MOFL		
	SET	1,M	
	BIT	7,A	
	JR	NZ,AUSGE	
	OR	A		
	RET	NZ
AUSGE:	RES	1,M
	XOR	A	
	RET
;
;GRAFIK-MODE?
;
MNULL:	LD	HL,MOFL
	LD	A,E
	BIT	2,M		;GRAFIKMODE?
	JR	Z,MSM1
	CP	8	
	JR	Z,MSM1
	CP	9	
	JR	Z,MSM1		;CL+CR KEIN GRFK.Z.
	ADD	A,80H		;SONST 80H ADDIEREN
	LD	E,A
;
;CAPS-TASTATURUMSCHALTUNG
; 
MSM1:	BIT	4,M
	JR	Z,MTE		;KEINE UMSCHAL. BIT 4=0
	LD	A,E
	CP	41H		;C-FLAG GES. FUER A<41H
	JR	C,MTE
;
;KONVERTIERUNG GROSS <-> KLEIN
;
MT5:	CP	5EH		;C-FLAG GESETZT FUER A<5BH
	JR	NC,MT61
	ADD	A,20H
	LD	E,A
	JR	MTE
MT61:	CP	61H		;C-FLAG GES. FUER A<61H (a)
	JR	C,MTE
	CP	7EH		;C-FLAG GES. FUER A<7BH
	JR	NC,MTE
	SUB	20H
	LD	E,A
;
;TASTATURBEEPIMPULS
; 
MTE:	LD	A,E
	LD	(MLZ),A		;LETZTES ZEICHEN =(0004)
	SEt	7,M
	BIT	5,M		;BEEP ON?
	JR	Z,MLED1
	LD	BC,2039H	;ZK
PEEP:	PUSH	BC
MPE3:	PUSH	BC
	LD	A,C		;TONH.=0->PAUSE
	OR	A
	LD	A,0FH
	JR	Z,MPE4
	LD	A,0EH
MPE4:	OUT	(LATCH),A
	POP	BC		;C=f, B=t
	PUSH	BC
MPE1:	DEC	C		;NEGATIVE HALBWELLE
	JR	NZ,MPE1
	LD	A,0FH
	OUT	(LATCH),A
	POP	BC
	PUSH	BC
MPE2:	DEC	C		;POSITIVE HALBWELLE
	JR	NZ,MPE2
	POP	BC
	DJNZ	MPE3
	POP	BC
;
;NEUSETZEN FLIP-FLOP's
;
SETFF:	PUSH	HL
	LD	HL,MOFL
	LD	A,0EH		;LED-FLIP-FLOP
	OUT	(LATCH),A	;NEU SETZEN 
	BIT	3,M
	JR	Z,MGR
	LD	A,0CH
	OUT	(LATCH),A
MGR:	BIT	4,M
	POP	HL
	JR	Z,MLED1
	LD	A,0DH
	OUT	(LATCH),A
MLED1:	LD	A,7H
	OUT	(LATCH),A
	LD	A,E
	OR	A
	RET
;
	IF	K7652
;
;FUNKTIONSTASTEN-POS.TABELLE
;
FTAB:	DB	2EH		;GRAFIK ON/OFF
	DB	35H		;SHIFT-LOCK
	DB	41H+60H		;BEEP ON/OFF
	DB	43H		;MON-RET/RST 38H
	DB	48H		;32/64
	DB	4CH		;ZG-UMSCH.
	DB	0ABH		;2 MHZ
	DB	0ACH		;4 MHZ
	DB	0ADH		;JMP 100
;
;FUNKTIONSTASTEN-ADRESSTABELLE
;
FATAB:	DW	GRF
	DW	SLOCK
	DW	BEEP
	DW	MONRET
	DW	B3264
	DW	ZGUM
	DW	F2MHZ
	DW	F4MHZ
	DW	J100
;
;TASTENBELEGUNGSTABELLE
;ERSTE SHIFTEBENE
; 
TAB:	DB	31H		;A4/1
	DB	51H		;B2/Q
	DB	41H		;C2/A
	DB	59H		;D3/Y
	DB	32H		;A5/2
	DB	57H		;B3/W
	DB	53H		;C3/S
	DB	58H		;D4/X
	DB	33H		;A6/3
	DB	45H		;B4/E
	DB	44H		;C4/D
	DB	43H		;D5/C
	DB	34H		;A7/4
	DB	52H		;B5/R
	DB	46H		;C5/F
	DB	56H		;D6/V
	DB	35H		;A8/5
	DB	54H		;B6/T
	DB	47H		;C6/G
	DB	42H		;D7/B
	DB	36H		;A9/6
	DB	5AH		;B7/Z
	DB	48H		;C7/H
	DB	4EH		;D8/N
	DB	37H		;A10/7
	DB	55H		;B8/U
	DB	4AH		;C8/J
	DB	4DH		;D9/M
	DB	38H		;A11/8
	DB	49H		;B9/I
	DB	4BH		;C9/K
	DB	2CH		;D10/,
	DB	39H		;A12/9
	DB	4FH		;B10/O
	DB	4CH		;C10/L
	DB	2EH		;D11/.
	DB	30H		;A13/0
	DB	50H		;B11/P
	DB	5CH		;C11/\ oder OE	
	DB	2DH		;D12/-
	DB	7EH		;A14/~ oder SZ	
	DB	5DH		;B12/] oder UE
	DB	5BH		;C12/[ oder AE
	DB	60H		;A15/` 
	DB	2BH		;B13/+
	DB	23H		;C13/DOPPELKREUZ
	DB	0		;A3/GRAPHIK
	DB	40H		;D2/KOMMERZIELLES A
	DB	3EH		;A16/>
	DB	0DH		;D14/ENTER/CTRL-M
	DB	5		;E2/CURSOR HOCH /CTRL-E
	DB	8		;E6/CURSOR LINKS /CTRL-H
	DB	20H		;E4/LEERTASTE
	DB	0		;B1/CAPS ON/OFF
	DB	18H		;E1/CURSOR RUNTER /CTRL-J
	DB	4		;E7/CURSOR RECHTS /CTRL-D
	DB	0
Š	DB	0
	DB	0
	DB	0
	DB	0
	DB	0
	DB	0		;D1,D13/SHIFT
	DB	0		;C1/CTRL-TASTE
	DB	0		;A2/SLOW-FAST
	DB	7		;B14/DEL /CTRL-G
	DB	3		;A17/BREAK /CTRL-C	
	DB	0		;A1/MONITORRUECKKEHR
	DB	82H		;S4/'B:'
	DB	81H		;S3/'A:'
	DB	9		;E5/TAB /CTRL-I
	DB	1BH		;E3/ESC
	DB	12H		;S1/CTRL-R (32/64-ZEICHEN)
	DB	83H		;S5/'BIOS'  
	DB	92H		;S7/'DIR'
	DB	16H		;S9/INS /CTRL-V
	DB	13H		;S2/CTRL-S (2.ZG)
	DB	84H		;S6/'D:'
	DB	88H		;S8/'COPY'
	DB	7FH		;S10/DEL
	DB	0
	DB	0
	DB	0
	DB	0
	DB	0
	DB	0
	DB	0
	DB	0
	DB	0
	DB	0
	DB	0
	DB	0
	DB	0
	DB	0
	DB	0
	DB	0
;
;ZWEITE SHIFTEBENE
; 
TAB2:	DB	21H		;A4/!
	DB	71H		;B2/q
	DB	61H		;C2/a
	DB	79H		;D3/y
	DB	22H		;A5/"
	DB	77H		;B3/w
	DB	73H		;C3/s
	DB	78H		;D4/x
	DB	40H		;A6/@
	DB	65H		;B4/e
	DB	64H		;C4/d
	DB	63H		;D5/c
	DB	24H		;A7/DOLLAR
	DB	72H		;B5/r
	DB	66H		;C5/f
	DB	76H		;D6/v
	DB	25H		;A8/%
	DB	74H		;B6/t
	DB	67H		;C6/g
	DB	62H		;D7/b
	DB	26H		;A9/&
	DB	7AH		;B7/z
	DB	68H		;C7/h
	DB	6EH		;D8/n
	DB	2FH		;A10//
	DB	75H		;B8/u
	DB	6AH		;C8/j
	DB	6DH		;D9/m
	DB	28H		;A11/(
	DB	69H		;B9/i
	DB	6BH		;C9/k
	DB	3BH		;D10/;
	DB	29H		;A12/)
	DB	6FH		;B10/o
	DB	6CH		;C10/l
	DB	3AH		;D11/:
	DB	3DH		;A13/=
	DB	70H		;B11/p
	DB	7CH		;C11/| oder oe
	DB	5FH		;D12/_
	DB	3FH		;A14/?
	DB	7DH		;B12/} oder ue
	DB	7BH		;C12/{ oder ae
	DB	5EH		;A15/CONTROL ZEICHEN ^
	DB	2AH		;B13/*
	DB	27H		;C13/'
	DB	0		;A3/INVERS
	DB	1EH		;D2/NL
	DB	3CH		;A16/<
	DB	0DH		;D14/ENTER/CR
	DB	0BH		;E2/C.HOCH
	DB	8H		;E6/C.LINKS
	DB	20H		;E4/LEERTASTE
	DB	0		;B1/CAPS
	DB	0AH		;E1/C.RUNTER
	DB	9H		;E7/C.RECHTS
	DB	0
	DB	0
Š	DB	0
	DB	0
	DB	0
	DB	0
	DB	0		;D1,D13/SHIFT
	DB	0		;C1/CTRL-TASTE
	DB	0		;A2/J 100
	DB	0		;B14/BEEP
	DB	10H		;A17/HARDCOPY /CTRL-P
	DB	0		;A1/
	DB	94H		;S4/'TYPE' 
	DB	90H		;S3/'POWER' 
	DB	14H		;E5/AB C. LOESCHEN
	DB	19H		;E3/DELLINE /CTRL-Y
	DB	0		;S1/2 MHZ
	DB	8FH		;S5/'POWER COPY'
	DB	85H		;S7/'EXIT'
	DB	87H		;S9/'GROUP'
	DB	0		;S2/4 MHZ
	DB	86H		;S6/'POWER STAT'
        DB	99H		;S8/'TEST'
	DB	9AH		;S10/'POWER SIZE ***'
	DB	0		
	DB	0
	DB	0
	DB	0
	DB	0
	DB	0
	DB	0
	DB	0
	DB	0
	DB	0
	DB	0
	DB	0
	DB	0
	DB	0
	DB	0
	DB	0
;
	ENDIF
;
	IF 	K7659
;
;FUNKTIONSTASTEN-POS.TABELLE
;
FTAB:	DB	52H		;GRAFIK ON/OFF
	DB	35H		;SHIFT-LOCK
	DB	55H		;BEEP ON/OFF
	DB	47H		;2 MHz
	DB	48H		;4 MHz
	DB	49H		;32*32/16*64
	DB	4AH		;ALTERNATIV-ZG
	DB	43H		;MON-RET/RST 38H
	DB	0A7H		;JMP 100H
;
;FUNKTIONSTASTEN-ADRESSTABELLE
;
FATAB:	DW	GRF
	DW	SLOCK
	DW	BEEP
	DW	F2MHZ
	DW	F4MHZ
	DW	B3264
	DW	ZGUM
	DW	MONRET
	DW	J100
;
;TASTENBELEGUNGSTABELLE
;ERSTE SHIFTEBENE
;
TAB:	DB	31H		;E0/1
	DB	51H		;D1/Q
	DB	41H		;C1/A
	DB	59H		;B1/Y
	DB	32H		;E2/2
	DB	57H		;D2/W
	DB	53H		;C2/S
	DB	58H		;B2/X
	DB	33H		;E3/3
	DB	45H		;D3/E
	DB	44H		;C3/D
	DB	43H		;B3/C
	DB	34H		;E4/4
	DB	52H		;D4/R
	DB	46H		;C4/F
	DB	56H		;B4/V
	DB	35H		;E5/5
	DB	54H		;D5/T
	DB	47H		;C5/G
	DB	42H		;B5/B
	DB	36H		;E6/6
	DB	5AH		;D6/Z
	DB	48H		;C6/H
	DB	4EH		;A6/N
	DB	37H		;E7/7
	DB	55H		;D7/U
	DB	4AH		;C7/J
	DB	4DH		;B7/M
	DB	38H		;E8/8
	DB	49H		;D8/I
	DB	4BH		;C8/K
	DB	2CH		;B8/,
	DB	39H		;E9/9
	DB	4FH		;D9/O
	DB	4CH		;C9/L
	DB	2EH		;B9/.
	DB	30H		;E10/0
	DB	50H		;D10/P
	DB	5CH		;C10/OE
	DB	2DH		;B10/-
	DB	7EH		;E11/SZ
	DB	5DH		;D11/UE
	DB	5BH		;C11/AE
	DB	3CH		;E12/<
	DB	2BH		;D12/+
	DB	23H		;C12/DOPPELKREUZ
	DB	5EH		;E00/CTRL-ZEICHEN
	DB	40H		;B0/KOMMERZIELLES A
	DB	3EH		;E13/>
	DB	0DH		;B13/ENTER/CTRL-M
	DB	5		;A1/CURSOR HOCH /CTRL-E
	DB	8		;A10/CURSOR LINKS /CTRL-H
	DB	20H		;A5/LEERTASTE
	DB	0		;D0/CAPS ON/OFF
	DB	18H		;A0/CURSOR RUNTER /CTRL-X
	DB	4		;A11/CURSOR RECHTS /CTRL-D
	DB	0
Š	DB	0
	DB	0
	DB	0
	DB	0
	DB	0
	DB	0		;B99,B11/SHIFT
	DB	0		;C0/CTRL-TASTE
	DB	0
	DB	7		;D13/ROTES C /DEL=^G
	DB	9		;E14/ROTES L /TAB
	DB	0		;F01/MONITORRUECKKEHR
	DB	1BH		;F02/ESCAPE
	DB	7BH		;F03/GESCHW.KLAMMER AUF
	DB	7DH		;F04/     -"-       ZU
	DB	0		;F06/2MHz
	DB	0		;F07/4MHz
	DB	0		;F08/32/64 
	DB	0		;F09/ZG-UMSCHALTUNG
	DB	15H		;F10/CTRL-U
	DB	19H		;F11/CTRL-Y
	DB	16H		;F12/CTRL-R/INSERT=^V
	DB	7FH		;F13/CTRL-P/DEL
	DB	3H		;F14/BREAKTASTE /CTRL-C
	DB	6H		;F05/CTRL-F
	DB	0
	DB	0		;F53/GRAFIK ON/OFF
	DB	0		;E53
	DB	0		;D53/
	DB	0		;C53/PEEP ON/OFF 
	DB	0
	DB	13H		;E95/CTRL-S=STOP
	DB	1DH		;F95/CTRL-]
	DB	0
	DB	0
	DB	0
	DB	12H		;D,C95/WIPPE + /^R
	DB	0
	DB	3		;B95/WIPPE - /^C
	DB	0
;
;ZWEITE SHIFTEBENE
;
TAB2:	DB	21H		;E1/!
	DB	71H		;D1/q
	DB	61H		;C1/a
	DB	79H		;B1/y
	DB	22H		;E2/"
	DB	77H		;D2/w
	DB	73H		;C2/s
	DB	78H		;B2/x
	DB	40H		;E3/@
	DB	65H		;D3/e
	DB	64H		;C3/d
	DB	63H		;B3/c
	DB	24H		;E4/DOLLAR
	DB	72H		;D4/r
	DB	66H		;C4/f
	DB	76H		;B4/v
	DB	25H		;E5/%
	DB	74H		;D5/t
	DB	67H		;C5/g
	DB	62H		;B5/b
	DB	26H		;E6/&
	DB	7AH		;D6/z
	DB	68H		;C6/h
	DB	6EH		;B6/n
	DB	2FH		;E7//
	DB	75H		;D7/u
	DB	6AH		;C7/j
	DB	6DH		;B7/m
	DB	28H		;E8/(
	DB	69H		;D8/i
	DB	6BH		;C8/k
	DB	3BH		;B8/;
	DB	29H		;E9/)
	DB	6FH		;D9/o
	DB	6CH		;C9/l
	DB	3AH		;B9/:
	DB	3DH		;E10/=
	DB	70H		;D10/p
	DB	7CH		;C10/oe
	DB	5FH		;B10/_
	DB	3FH		;E11/?
	DB	7DH		;D11/}
	DB	7BH		;C11/{
	DB	5BH		;E12/ECK.KLAMMER AUF
	DB	2AH		;D12/*
	DB	27H		;C12/'
	DB	7CH		;E0/SENKR.STRICH
	DB	5CH		;B0/NEG.SCHRAEGSTRICH
	DB	5DH		;E13/ECK.KLAMMER ZU
	DB	0DH		;C,B13/ENTER/CR
	DB	05H		;A01/C.HOCH CPM
	DB	13H		;A10/C.LINKS CPM
	DB	20H		;A05/LEERTASTE
	DB	0		;D00/SM-TAST. ON/OFF
	DB	18H		;A00/C.RUNTER CPM
	DB	04H		;A11/C.RECHTS CPM
	DB	0
	DB	0
Š	DB	0
	DB	0
	DB	0
	DB	0
	DB	0		;B99/B11/SHIFT
	DB	0		;C00/CTRL-TASTE
	DB	0
	DB	7H		;D13/ROTES C/CTRL-G
	DB	13H		;E14/ROTES L/CTRL-S
	DB	0		;F01/MON.RUECK.
	DB	1BH		;F02/ESCAPE 
	DB	60H		;F03/NEG.ANSTRICH 
	DB	7EH		;F04
	DB	0		;F06/JMP 100H
	DB	0		;F07
	DB	0		;F08
	DB	0		;F09
	DB	0FH		;F10/CTRL-O
	DB	18H		;F11/CTRL-X
	DB	11H		;F12/CTRL-Q
	DB	1FH		;F13
	DB	3H		;F14/BREAKTASTE /CTRL-C
	DB	1EH		;F05/NL(SIF1000)
	DB	0
	DB	0		;F53/GRAFIK ON/OFF
	DB	0		;E53
	DB	0		;D53
	DB	0		;C53/PEEP ON/OFF
	DB	0
	DB	1CH		;E95/
	DB	1DH		;F95/
	DB	0
	DB	0
	DB	0
	DB	19H		;D,C95/WIPPE +/CTRL-Y
	DB	0
	DB	18H		;B95/WIPPE - /CTRL-X
	DB	0
	ENDIF
;
SFELD:	DB	81H
	DB	'A:',0DH
	DB	82H
	DB	'B:',0DH
	DB	83H
	DB	'C:',0DH
	DB	84H 
	DB	'DIR',0DH
	DB	85H 
	DB	'EXIT',0DH    
	
	DB	91H		;Q
	DB	0BH,'QJ'
	DB	86H		;S
	DB	'STAT',0DH
	
	DB	8AH		;$
	DB	'SIZE ***',0DH
	
	DB	94H		;T
	DB	'TYPE'
	DB	90H		;P
	DB	'POWER'

	DB	8FH		;O
	DB	'COPY',0DH

        DB      80H             ;->
	DB	4,4,4,4,4,4,4,4,4,4

        DB      88H             ;<-
        DB      8,8,8,8,8,8,8,8,8,8

END$F:	DB	0		
;Arbeitszellen fuer Tastatur
NEXT$:	DW	0		;STRING-ARBEITSZELLE
MLZ:	DB	0		;MERKE LETZTES ZEICHEN TASTATUR
MOFL:	DB	0		;MODEFLAGS
;POINTERFELD

TAPOI:	DW	TAB		;TASTATURTAB.-POINTER
PO$TA:	DW	SFELD		;STRINGTAB.-POINTER
LFTB:	DW	FATAB-FTAB	;LAENGE FU-TASTENTAB.
FTPOI:	DW	FTAB		;FU-TASTENPOS.-TAB.POINTER
FTAPO:	DW	FATAB		;FU-TASTENADR.-TAB.POINTER
ton:	db	0c9h		;Kutzschluss fuer Piep

