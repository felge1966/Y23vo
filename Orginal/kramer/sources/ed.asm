	;PN	ED

	cpu	z80

	TITLE "EDITOR Y23VO"


;VEREINBARUNGEN
CR: 	EQU	0DH 		;WAGENRUECKLAUF
LF: 	EQU	0AH 		;ZEILENVORSCHUB
ETX: 	EQU	3 		;END OF TEXT
CTRLD: 	EQU	4 		;ZEILE DOPPELN
DEL: 	EQU	7FH 		;ZEILE LOESCHEN
CAN: 	EQU	18H 		;ZEICHEN LOESCHEN
INS: 	EQU	13H 		;ZEICHEN EINFUEGEN
NUL: 	EQU	0FFH 		;TABELLENENDE
NL: 	EQU	1EH 		;NEUE ZEILE
SUB: 	EQU	1AH 		;ZEILE EINFUEGEN
RAME: 	EQU	7FFFH 		;STANDARDZUWEISUNG
RAMA: 	EQU	4000H
SRAM: 	EQU	0D39H
CIO:	EQU	13CH
RI: 	EQU	0E3H
CO: 	EQU	0E6H
WO: 	EQU	0E9H
LO:	EQU	0ECH
ERROR: 	EQU	0038H
CURS: 	EQU	0FF1H
TI:	EQU	10CH
PA0: 	EQU	4DCH
FCGA: 	EQU	7AH
PCHK: 	EQU	518H
STR: 	EQU	128H
INK: 	EQU	36BH
LKOM:	EQU	352H
OUTK:	EQU	38CH
BRAM:	EQU	0FC00H 		;BILDSCHIRM-RAM

	ORG	0C000H
	PUSH	AF
	PUSH	BC
 	PUSH	DE
	PUSH	HL
	LD	HL,ATEXT
	CALL 	STR
	CALL	TI 		;TI
	CP 	'Y'
	JR	NZ,RESTR
;UEBERTRAGEN SYSTEMTABELLEN
	LD 	HL,STAB		;QUELLE
	LD	DE,RAMEZ 	;ZIEL
	LD 	BC,LSTAB
	LDIR
	LD 	HL,(RAMEZ)	;ENDE SETZEN
	LD 	A,ETX
	LD 	(HL), A
;RUECKSETZEN ZEICHENZAEHLER
RESTR:	LD 	HL,(RAMEZ)
	LD	(ZEZL1),HL
START:
;STARTADRESSE RETTEN
	LD 	HL,START
	PUSH	HL
	LD 	HL,PIONT
	CALL	STR
;EINGABE KOMMANDO
	LD 	HL,JPTBL
	CALL 	TI		;TI
	CALL	FCGA
	JP	C,E1
	JP	(HL)



JPTBL:
	DB	'A'
	DW	ASGN
	DB	'D'
	DW	DISPL
	DB	'E'
	DW	EDIT
	DB	'G'
	DW	GO
	DB	'I'
	DW	INK
	DB	'L'
	DW	LKOM
	DB	'O'
	DW	OUTK
	DB	'P'
	DW	PRINT
	DB	'Q'
	DW	ENDE
	DB	'R'
	DW	READ
	DB	'U'
	DW	UP
	DB	'W'
	DW	WRITE
	DB	'T'
	DW	TOP
	DB	NUL



ENDE:
	POP	HL		;STARTADR. KOMMANDO
	POP	HL
	POP	DE
	POP	BC
	POP	AF
	RET

;KOMMANDO SPEICHERZUWEISUNG
ASGN:
	LD	HL,EOM
	CALL	STR
	CALL	PCHK
	RET	Z
	CALL	PA0
	LD	(RAMEZ),HL
	LD	(ZEZL1),HL
	LD 	A,ETX 		; ENDE SETZEN
	LD 	(HL),A
	LD 	HL,SOM
	CALL	STR
	CALL 	PCHK
	RET	Z
	CALL	PA0
	LD	(RAMAZ),HL
	RET

;AUSGABE ALLER ZElLEN AUF KANAL LO
PRINT:
	CALL	CRLF
	LD 	HL,(RAMEZ)
P1:	LD	A,(HL)
	CP 	ETX
	RET	Z		;END OF FILE
	CP	NL		;NEW LINE
	CALL	Z,P3		;UMWANDELN
	DEC	HL
	LD	C,A
	CALL	LO
	JR	P1
P3:	LD	C,CR
	CALL	LO
	LD	A,LF
	RET


;LESEN BIS ENDE VON KANAL RI

READ:
	LD	HL,(ZEZL1)
	INC	HL
R1:	DEC	HL
	CALL	KOPU
R2:	CALL	RI
	AND	7FH
	CP	CR
	JR	Z,LZNL
	CP	LF
	JR	Z,LZNL
R3:	LD	(HL),A
	CP	ETX		;TEXTENDE
	JR	NZ,R1
	LD	(ZEZL1),HL
	RET



;PRUEFEN LETZTES ZEICHEN NL=?
;J:CR,LF UEBERLESEN
;N:CR,LF UMWANDELN
LZNL:	INC	HL
	LD	A,(HL)
	DEC	HL
	CP	1EH
	JR	Z,R2
	LD	A,1EH		;CR O. LF ERSETZEN
	JR	R3



;AUSGABE EINER ANZAHL ZEICHEEN AUF KANAL WO
;MIT AUSGABE ENDE-ZEICHEN
WRITE:
	CALL	PCHK
	JR	C,W1		;ALLE
	CALL	PA0
W2:	EX	DE,HL
W3:	CALL	W1Z
	CP	ETX		;TEXTENDE
	RET	Z
	DEC	DE
	LD	A,E
	OR	D
	JR	NZ,W3
	LD	C,ETX		;ENDE SETZEN
	CALL	WO
	RET
W1:	LD	HL,0FFFFH
	JR	W2


;AUSGABE EINER ZEILE AUF KANAL WO
;IN: (ZEZL1)
;OUT:(ZEZL1)-ZEILENLAENGE

W1Z:
	LD	HL,(ZEZL1)
	INC	HL
W1Z1:	DEC	HL
	LD	A,(HL)
	PUSH	AF
	LD	C,A
	CALL	WO
	POP	AF
	CP	ETX		;TEXTENDE
	RET	Z
	CP	NL		;ZEILENENDE
	JR	NZ,W1Z1
	DEC	HL
	LD	(ZEZL1),HL
	RET

;AUSGABE EINER ANZAHL ZEILEN AUF BILDSCHIRM
;MIT EINSTELLUNG (ZEZL1)

DISPL:
D4:	CALL	PCHK
	RET	C		;ENDE BEI CR
	JR	Z,DSPL1		;EINZELN BEI LEERZEICHEN
	CALL	PA0
DSPL2:	CALL	DISP1
	CP	ETX		;ENDE ?
	RET	Z
	DEC	HL
	LD	A,L
	OR	H
	JR	NZ,DSPL2
	RET

;AUSGABE ZEILENWEISE BEI LEERZEICHEN
DSPL1: 	CALL 	DISP1
	CP	ETX
	JR	NZ,D4
	RET



;DISPLAY FUER EINE ZEILE
;IN: (ZEZL1)=AKT. ZEICHEN
;OUT:(ZEZL1+1ZEILE),A=1EH ODER 4
DISP1:
	PUSH	BC
	PUSH	HL
	CALL	CRLF
	LD	HL,BRAM+960	;UNTERSTE BILDSCH.-ZEILE
	LD	(CURS),HL
	LD	HL,(ZEZL1)
D1:	LD	A,(HL)
	CP	ETX
	JR	Z,D2		;END OF FILE
	DEC	HL
	CALL	KOPU
	CP	NL
	PUSH	AF
	LD	C,A
	CALL	NZ,CO
	LD	A,C
	POP	AF
	JR	NZ,D1
	LD	(ZEZL1),HL
D2:	POP	HL
	POP	BC
	RET


;TEXTE
ATEXT:	DB	CR
	DB	LF
	DB	"TE 01"
	DB	CR
	DB	LF
	DB	"NEW?  (N)"
	DB	0
EOM:	DB	CR
	DB	LF
	DB	"END OF MEMORY?  "
	DB	0
SOM:	DB	CR
	DB	LF
	DB	"START OF MEMORY?"
	DB	0
PIONT:	DB	CR
	DB	LF
	DB	">> "
	DB	0



;ZEICHENZAEHLER RUECKSTELLEN
;IN: (ZEZL1)=AKT.
;OUT: (ZEZL1)=(RAMEZ)
TOP:
	LD	HL,(RAMEZ)
	LD	(ZEZL1),HL
	RET



;KONTROLLRECHNUNG PUFFER
;IN: <HL>=ZU PRUEFENDE ZEILE
;    <HL>-<RAMAZ>=>0 SONST FEHLER
;KILL: AF
KOPU:
	PUSH	HL
	PUSH 	DE
	EX 	DE,HL
	LD 	HL,(RAMAZ)
	EX 	DE,HL
	OR	A
	SBC	HL,DE
	CALL	M,E2 		;PUFFER VOLL
	POP	DE
	POP	HL
	RET



;EDIT-KOMMANDO
EDIT:
;AKTUELLEZE ILE IN ANZEIGE
ED1:	CALL	DISP1
	CP	ETX
	CALL	NZ,ZLOE
ED2: 	CALL	CIO
	CP	CR
	JR	Z,ED4
	CP	SUB
	JR	Z,ED5
	CP	LF
	JR	Z,ED3		 ;NAECHSTE
	CP	DEL
	CALL	Z,PULOE 	;ANZEIGE LOESCHEN
	CP 	CTRLD 		;ZEILE DOPPELN
	CALL	Z,DUPL
	CP	CAN
	JR	Z,CHDEL
	CP	INS
	JR	Z,CHINS
	LD	C,A
	CALL	CO
	JR	ED2


;AUSWERTUNG LF
ED3: 				;NEUE ZEILE
	CALL	ED4
	JR	ED1

;AUSWERTUNG CR,ENDE EDITKOMMANDO
ED4:	CALL	LANG
	CALL	MOVE
	CALL	APZP
	RET
;
;AUSWERTUNG CONTROL Z=SUB
;ZEILEN EINFUEGEN
ED5:
	CALL	ED4
	JR	ED2


;AUSWERTUNG CONTROL X=CANCEL
;ZEICHEN AUF BILDSCHIRM LOESCHEN
CHDEL:
	CALL	LANG
	CALL	REST
	LD	HL,(CURS)
	PUSH	HL
	POP	DE
	INC	HL
	LDIR
	LD	A,' '		;ENDE LOESCHEN
	LD	(HL),A
	JR	ED2

;AUSWERTUNG CONTROL S
;ZEICHEN AUF BILDSCHIRM EINFUEGEN
CHINS:
	LD	HL,(CURS)
	PUSH	HL
	LD 	A,(HL)
	AND	7FH		;CURSOR LOESCHEN
	LD	(HL),A
	CALL	LANG
	CALL	REST
	LD	HL,BRAM+1022
	LD	DE,BRAM+1023
	LDDR
	POP	HL
	LD	A,0A0H		;CURSOR SETZEN
	LD	(HL),A
	JR	ED2



;AUSWERTUNG CONTROL D
;ZEILE DOPPELN
DUPL:
	PUSH	BC
	PUSH	DE
	PUSH	HL
	LD	HL,BRAM+380H	;VORLETZTE ZEILE
	LD	DE,BRAM+3C0H	;LETZTE ZEILE
	LD	BC,3EH		;LAENGE
	LDIR
	LD	(CURS),DE
	LD	A,020H		;LEERZ.
	POP	HL
	POP	DE
	POP	BC
	RET



;ERMITTLUNG DER LAENGE ANZEIGEPUFFER
;IN: (CURS)=AKT.ZEILE
;OUT: A=LAENGE
LANG:	PUSH	HL
	LD	HL,(CURS)
	LD	A,L
	LD	HL,BRAM+960	 ;ANFANG LETZTE BILDSCH.-ZEILE.
	SUB	L
	POP	HL
	RET

;
;VERSCHIEBUNG PUPPERINHALT NACH UNTEN UM 1 ZEILE
;IN: A ZEILENLAENGE
;<BC> = ANZAHL BIS ENDE
;<DE> =ZIELADR.=ZEZL1-ANZAHL-ZEILENLAENGE
;<HL> = QUELLADRESSE=ZEZL1-ANZAHL
MOVE:
	OR	A		 ;SONDERFALL A=0
	RET	Z
	PUSH	HL
	PUSH	DE
	PUSH	BC
	PUSH	AF
	LD	HL,(ZEZL1)
	LD	BC,0
MOV1:	LD 	A,(HL)
	DEC	HL
	CP	ETX
 	INC	BC
	JR	NZ,MOV1
	INC	HL
	PUSH	HL		 ;<HL>=TEXTENDE
	POP	DE
;BERECHNUNG ZIELADRESSE
	POP	AF
	PUSH	BC
	LD	B,0
	LD	C,A
	INC	C		;PLATZ F.1EH
	OR	A
	SBC	HL,BC
	CALL	KOPU
	POP	BC
	EX	DE,HL		;ZIEL/QUELLE
	LDIR
	POP	BC
	POP	DE
	POP	HL
	RET






;GO ZEILENNUMMER
GO:
	CALL	PCHK
	JR	C,G8		;ALLE
	RET	Z
	CALL	PA0		;ANZAHL IN HL
G7:	LD	DE,(ZEZL1)
	EX	DE,HL
G1:	CALL	G1Z		;GO 1 ZEILE
	RET	C		;ENDE
	DEC	DE
	LD	A,E
	OR	D
	JR	NZ,G1
G5:	LD	(ZEZL1),HL
	RET

UP:
	CALL	PCHK
	RET	Z
	CALL	PA0
	LD	DE,(ZEZL1)
G4:	CALL	G1ZZ
	DEC	HL
	LD	A,L
	OR	H
	JR	NZ,G4
	EX	DE,HL
	JR	G5

G8:	LD	HL,0FFFFH	;ALLE
	JR	G7

;GO 1 ZEILE ZURUECK
;IN: HL= AKT. PUFFERZELLE
;OUT: A=1EH AM PUFFERENDE
G1ZZ:	INC	DE
G6:	INC	DE
	LD	A,(DE)
	CALL	TEPUE
	CP	NL
	JR	NZ,G6
	DEC	DE
	RET


;GO 1 ZEILE
;OUT: CY=1 BEI EOF
G1Z:	LD	A,(HL)
	CP	ETX
	JR	Z,G1Z1
	DEC	HL
	CALL	KOPU
	CP	NL
	JR	NZ,G1Z
	RET

G1Z1:	SCF
	RET


CRLF:	PUSH	AF		;AUSGABE NEUE ZEILE BILDSCHIRM
	PUSH	BC
	LD	C,NL
	CALL	CO
	POP	BC
	POP	AF
	RET



;ANZEIGE LOESCHEN
PULOE:
	PUSH	BC
	PUSH	HL
	LD	B,41H
	LD	A,' '
	LD	HL,BRAM+959	;LETZTE BILDZEILE-1ZEICH.
	LD	(CURS),HL
PUL1:	LD	(HL),A
	INC	HL
	DJNZ	PUL1
	POP	HL
	POP	BC
	RET


;BILDSCH.IN ZEICHENPUFFER UEBERTRAGEN
;IN:  A=LAENGE
APZP:
	OR	A
	RET	Z
	PUSH	BC
	LD	B,A
	LD	DE,BRAM+960	;LETZTE BILDSCH.-ZEILE
	LD	HL,(ZEZL1)	;ZIEL
APZP1:	LD	A,(DE)
	AND	7FH		;CURSOR AUSBLENDEN
	LD	(HL),A
	INC	DE
	DEC	HL
	DJNZ	APZP1
	LD	(HL),NL
	DEC	HL
	LD	(ZEZL1),HL
	CALL	CRLF
	POP	BC
	RET

;1 ZEILE IM PUFFER LOESCHEN
;IN: <ZEZL1>=ANFANG NAECHSTE ZEILE
;NAECHSTER SPEICHERPLATZ 1EH
ZLOE:
	PUSH	AF
	PUSH	BC
	PUSH	DE
	PUSH	HL
	LD	HL,(ZEZL1)
	PUSH	HL
;BERECHNUNG ANZAHL,-> BC
	LD	BC,0
	LD	A,ETX
ZL1: 	LD	D,(HL)
	INC	BC
	DEC	HL
	CP	D
	JR	NZ,ZL1
	POP	HL
	PUSH	HL
	POP	DE		;<HL>=<ZEZL1>
;EINSTELLUNG DE AUF ZIELADRESSE
	CALL	G1ZZ
	LD	(ZEZL1),DE
	LDDR
	POP	HL
	POP	DE
	POP	BC
	POP	AF
	RET

;TEST PUFFERENDE
TEPUE:	PUSH	HL
	LD	HL,(RAMEZ)
	OR	A
	SBC	HL,DE
	CALL	M,TEP1
	POP	HL
	RET

TEP1:
	LD	DE,(RAMEZ)
	INC	DE
	LD	A,NL		;PUFFERENDE
	ReT


REST:
;IN: A - LABNGE
;OUT: B=0,C=ANZAHL BIS ZEILENENDE

	PUSH	AF
	LD	B,A
	LD	A,3FH
	SUB	B
	LD	B,0
	LD	C,A
	POP	AF
	RET


;FEHLERMELDUNGEN

E1:	LD	HL,3145H	;KOMMANDOFEHLER
ERA:	LD	(BRAM+1022),HL	;PLATZ FEHLERANZEIGE
	RET

E2:	LD	HL,3245H	;PUFFER VOLL
	CALL	ERA
	LD	HL,(RAMAZ)
	LD	(HL),ETX
	JP	ERROR

E3:	LD	HL,3345H	;ADRESSE O.PARAMETER FALSCH
	JR	ERA


;SYSTEMTABELLE

STAB:
	DW	RAME 		;RAMENDE
	DW	RAMA		;RAMANFANG
	DW	RAME		;RAM-ZUWEISUNG
LSTAB:	EQU $-STAB




	ORG	SRAM

RAMEZ:	DS	2		;RAMENDEZELLE
RAMAZ:	DS	2		;RAMANFANG
ZEZL1:	DS	2		; RAME  ;ZEICHENZAEHLER1
	END
